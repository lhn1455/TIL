# Introduction to Smart Contracts
- [A Simple Smart Contract](#a-simple-smart-contract)
- [Blockchain Basics](#blockchain-basics)
- [The Ethereum Virtual Machine](#the-ethereum-virtual-machine)


## A Simple Smart Contract
예제 설명 ) 변수 값을 지정하고 다른 컨트랙트에서 접근하도록 하기 위해 이것을 공개하는 코드

### Storage Example
```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.4.16 <0.9.0;

contract SimpleStorage {
    uint storedData;

    function set(uint x) public {
        storedData = x;
    }

    function get() public view returns (uint) {
        return storedData;
    }
}
```

```solidity
// SPDX-License-Identifier: GPL-3.0
```
> The first line tells you that the source code is licensed under the GPL version 3.0. Machine-readable license specifiers are important in a setting where publishing the source code is the default.

```solidity
pragma solidity >=0.4.16 <0.9.0;
```
> 솔리디티의 버전 정보   
- pragma :  소스코드를 어떻게 다루는지에 대한 컴파일러 명령어    

<br>
<br>

솔리디티 관점에서 컨트랙트는 코드(함수)의 집합체이며, 이더리움 블록체인 안에서 특정한 주소를 가진 데이터(상태)들의 집합체이다.

- `uint storedData` : storedData라는 상태변수를 선언 (타입 : uint - 부호가 없는 256bits의 정수)

이것을 데이터베이스가 관리하는 함수 코드들을 부름으로써 쿼리를 날리고, 바꿀수 있는 데이터베이스 안의 single Slot이라고 생각할 수 있다.   
예를 들어, 컨트랙트는 변수의 값을 수정하고 검색하기위해 `set` 함수와 `get` 함수를 정의할 수 있다.   

현재 컨트랙트에 멤버변수(상태변수와 같은)에 접근하기 위해서,
접두사로 `this.`를 붙일 필요가 없다. 단지 이것을 이름에 기반하여 직접 접근할 수 있다.   

다른 언어와는 다르게, 솔리디티에서 `this.`을 생략하는 것은 스타일의 문제가 아닌 멤버에 접근하기 위한 완전히 다른 하나의 방식이다.   

이 컨트랙트는 이더리움에 기반한 것이기 때문에, 누구나 이 컨트랙트에 데이터를 담아서 배포할 수 있으며, 배포된 것에 접근할 수 있다.   

누구든지 다른 값을 `set`함수에 넣을 수 있고 이것은 내가 넣었던 값을 덮어 쓸 수 있다. 하지만 그 값은 여전히 블록체인 속에 존재한다. (추후에 오직 값을 너만 바꿀 수 있도록 하기위해서 어떻게 접근제한자를 부여할 수 있는지 볼것이다.)

> 모든 식별자(컨트랙트명, 함수명, 변수명 등)는 ASCII 코드로 제한된다. string 변수안에 데이터를 UTF-8로 인코딩된 데이터를 보관할 수 있다.

## Subcurrency Example

다음의 컨트랙트는 암호화폐의 가장 간단한 형태를 구현한 것이다. 이 컨트랙트는 오직 이것의 생성자만 새로운 코인을 만들 수 있도록 한다. (권한있는 사람도 가능). 누구든지 username, password를 등록하는 절차 없이 코인을 다른사람에게 보낼 수 있다. 필요한 것은 이더리움 키쌍(key-pair) 뿐이다.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;

contract Coin {
    // The keyword "public" makes variables
    // accessible from other contracts
    address public minter;
    mapping (address => uint) public balances;

    // Events allow clients to react to specific
    // contract changes you declare
    event Sent(address from, address to, uint amount);

    // Constructor code is only run when the contract
    // is created
    constructor() {
        minter = msg.sender;
    }

    // Sends an amount of newly created coins to an address
    // Can only be called by the contract creator
    function mint(address receiver, uint amount) public {
        require(msg.sender == minter);
        balances[receiver] += amount;
    }

    // Errors allow you to provide information about
    // why an operation failed. They are returned
    // to the caller of the function.
    error InsufficientBalance(uint requested, uint available);

    // Sends an amount of existing coins
    // from any caller to an address
    function send(address receiver, uint amount) public {
        if (amount > balances[msg.sender])
            revert InsufficientBalance({
                requested: amount,
                available: balances[msg.sender]
            });

        balances[msg.sender] -= amount;
        balances[receiver] += amount;
        emit Sent(msg.sender, receiver, amount);
    }
}
```
- 이 컨트랙트에서 소개하는 새로운 개념들
    - `address public minter;` : address타입의 상배 변수를 선언. (address타입 - 연산이 불가능한 160-bit값) / 컨트랙트의 주소를 담거나 외부 계정에 속한 키쌍의 퍼블릭키의 해시값을 담는데 적합

    - `public` : 컨트랙트의 밖에서 상태 변수의 현재 값에 접근하도록 허락하는 함수의 접근제어자 / 이 키워드가 없다면 다른 컨트랙트에서 이 변수에 접근할 수 없음.   
    
컴파일러에 의해 만들어진 함수의 코드는 다음과 같다.
```solidity
function minter() external view returns (address) { return minter; }
 ```
위와 같은 함수를 개별적으로 추가할 수는 있지만 동일한 이름을 가진 함수와 상태변수를 가져야 한다. (컴파일러가 이 작업을 대신 해주므로 할 필요없다.)

```solidity
mapping (address => uint ) public balances;
```
이 코드 또한 public한 상태변수를 만들지만 데이터 타입이 조금 더 복잡하다. 이 `mapping`타입은 address와 uint를 매핑시켜준다.   

매핑은 해시테이블 처럼 보여질 수 있다. 이 매핑은 시작부터 모든 가능한 키가 존재하고 그것은 0 값으로 초기화 되어있다. 하지만 이 매핑의 모든 키 리스트 또는 모든 값 리스트를 얻는것은 불가능하다. 매핑에 추가할 것들을 기록해라 그렇지 않으면 그것을 불필요한 context안에서 사용해라. 또는 더 좋은 방법은 리스트를 보관하거나 더 적합한 데이터 타입을 쓰는 것이다. 

public한 `getter function`은 매핑의 경우 더 복잡해진다. 그것은 아래의 코드와 같이 보일 수 있다.
```solidity
function balances(address _account) external view returns (uint) {
    return balances[_account];
}
```
하나의 계정의 잔액을 쿼리하는 함수를 사용할 수 있다.

`event Sent(address from, address to, uint amount)`는 event를 선언하고 있고 이것은 `send`함수의 마지막줄에 실행되고 있다. 웹 어플리케이션과 같은 이더리움 클라이언트는 많지 않은 비용으로 블록체인에 실행된 이벤트를 받을 수 있다. 이벤트가 실행 되자마자, 클라이언트는 `from`, `to` 그리고 `amount`를 받아 볼 수 있다. 이것은 트랜잭션 추적을 가능하게 만든다.   

이벤트를 듣기 위해서, 다음의 자바스크립트 코드를 사용할 수 있다. 이것은 `Coin` 컨트랙트 객체를 만들기 위해서 **web3.js**를 사용한다. 그리고 어떤 유저 인터페이스는 자동적으로 생성된 `balances` 함수를 호출할 수 있다.
```solidity
Coin.Sent().watch({}, '', function(error, result) {
    if (!error) {
        console.log("Coin transfer: " + result.args.amount +
            " coins were sent from " + result.args.from +
            " to " + result.args.to + ".");
        console.log("Balances now:\n" +
            "Sender: " + Coin.balances.call(result.args.from) +
            "Receiver: " + Coin.balances.call(result.args.to));
    }
})
```


constructor 생성자는 컨트랙트의 생성 동안 실행되는 스페셜한 함수이다. 그리고 이후에는 호출 될 수 없다. 이 경우, 생성자는 영구적으로 컨트랙트를 만든 사람의 주소를 보관한다. `msg`변수(`tx`, `block`과 함께)는 스페셜한 global 변수이다. 이 변수들은 블록체인에 접근하는 것을 허락하는 속성을 가지고 있다. `msg.sender`는 항상 현재 함수를 호출한 호출자의 주소이다.

컨트랙트를 구성하고 컨트랙트와 유저가 호출할 수 있는 함수는 `mint`와 `send`이다.   

`mint`함수는 다른 주소로 새롭게 만든 코인을 보낸다. `require`함수는 만약 충족하지 않는다면 모든 변화를 `revert`하는 조건 정의이다. 예를 들어 `require(msg.sender == minter`은 컨트랙트 만든 사람만 `mint`함수를 콜 할수 있다는 조건을 보장한다. 일반적으로 컨트랙트를 만든 사람은 그들이 원하는 만큼의 토큰을 mint할 수 있다. 하지만, 동시에 이것은 **overflow**라는 현상을 이끌 수 있다. 만약 `balances[receiver] += amount;`가 overflow를 일으킨다면, checked연산의 결함으로 인해, 트랜잭션은 revert될 수 있다. (예.임의의 정밀 연산에서 `balances[receiver] + amount`가 `uint (2**256 - 1)`보다 클 경우 등). 이것은 또한 `send`함수에서 `balances[receiver] += amount;`에 대하여 true이다.


Error 오류는 caller에게 왜 조건문 또는 실행이 실패되었는지에 대하여 더 많은 정보를 제공하도록 한다. 에러는 `revert statement`와 함께 사용된다. `revert statement`는 `reqire`함수와 비슷하게 모든 변화에 대하여 무작위로 중단시키거나 반환시킨다. 하지만 또한 실패가 쉽게 디버그 되거나 대처될 수 있도록 하기 위해서 caller(결과적으로 애플리케이션 프론트 엔드 또는 블록 explorer)에게 제공할 추가적인 데이터와 에러 이름을 제공하도록 한다.


`send`함수는 다른 누군가에게 코인을 보내기위해서 누구나(이미 이러한 코인을 가지고 있는 사람이라면) 사용할 수 있다. 만약 sender가 전송하기에 충분한 코인을 가지고 있지 않다면 `if` 조건문은 true를 판단한다. 결과적으로 `revert`는 sender가 `insufficientBalance`오류를 사용하여 오류의 세부사항을 제공받는 동안 실패를 실행하도록 만든다.

> 만약 어떤 주소로 코인을 보내기위해 이 컨트랙트를 사용한다면, 블록체인 explorer에서 주소를 찾을 때 어떤것도 볼 수 없을 것이다. 왜냐하면 코인을 전송했던 기록과 잔액의 변화는 오직 특정 코인 컨트랙트의 data storage에 저장되기 때문이다. `event`를 사용함으로써 새로운 코인의 잔액과 트랜잭션을 추적할 수 있는 "blockchain explorer"를 만들어낼 수 있다. 하지만 반드시 코인 주인의 어드레스가 아닌 코인 컨트랙트의 주소를 검사해야만 한다. 

# Blockchain Basics

블록체인이 프로그래머가 이해하기에 너무 어려운 개념은 아니다. 문제들(mining, hashing, 타원곡선 암호학, peer to peer network 등)이 단지 그 플랫폼을 위한 약속들이며 일련의 특징들을 제공하기위한 것이기 때문이다.
일단 이러한 특징을 이해하면, 근본적인 기술들을 이해하는것은 걱정할 필요가 없다.

## Transactions

블록체인은 전세계적으로 공유되어지는 transactional database이다. 이것은 모든사람들이 네트워크에 참여함으로써 데이터베이스에 접근하여 읽을 수 있다는 것을 의미한다. 만약 데이터베이스의 무언가를 바꾸길 원한다면 소위 transaction이라고 불리는 것을 만들어야 한다. 이것은 또한 모든 다른사람들로부터 받아들여져야 한다. 트랜잭션이라는 단어는 당신이 만들길 원하는 변화가 수행되거나 완전히 적용되었다는 것을 의미하지는 않는다. 게다가 당신의 트랜잭션이 데이터베이스에 적용되는 동안, 다른 어떤 트랜잭션도 그것을 바꿀 수 없다.

예를 들어, 전자화폐안에 모든 계정의 잔액 리스트가 있는 테이블이 있다고 가정하자. 만약 한 계정으로부터 다른 계정으로 transfer가 요구되었다면, 그 transaction의 본질은 그 계정의 총계가 다른 계정으로부터 빠졌는지 보증하는 것이다. 그 빠진 총계는 다른 계정에 더해진다. 만약 어떤 이유로 타겟 계정에 총계가 더해지는 것이 불가능해진다면, 소스 계정은 바뀌지 않는다.

게다가 transaction은 항상 암호학적으로 sender에 의해서 서명되어진다. 이것은 데이터베이스를 수정하기위해 접근하는 것을 막기위한 가장 간단한 방법이다. 전자화폐를 예로들면, 간단한 체크 과정은 계정의 키를 보유한 사람만이 그것으로 부터 돈을 이체할 수 있다는 것을 보장한다.

## Blocks
극복해야하는 하나의 가장 큰 장애물은 (비트코인 용어에서) "double-spend attack"이라 불리는 것이다. 만약 두개의 트랜잭션이 네트워크에 존재하고 둘다 한 계정을 비우길 원한다면 무슨일이 발생할까? 트랜잭션 중 하나(전형적으로 먼저 수락된 것)만이 타당성을 인정받을 수 있다. 문제는 peer-to-peer network에서 "먼저"는 객관적인 용어가 아니다.

이것에 대한 추상적인 답은 신경쓸 필요가 없다. 전세계적으로 수락된 트랜잭션의 순서는 충돌을 해결하기 위해 선택되어질 것이다. 트랜잭션은 "block"이라고 불리는 것 안에서 묶일 것이다. 그리고나서 그것은 실행되어지고 모든 참여노드로 분포될 것이다. 만약 두개의 트랜잭션이 서로 모순되어진다면, 두번째로 들어온 것은 거절될 것이고 블록의 부분이 될 수 없다.

이러한 블록은 시간에 대하여 선형적인 시퀀스를 형성한다. 즉, 블록체인이라는 단어는 여기서 파생된 것이다. 블록들은 상당히 규칙적인 간격으로 추가되어진다. - 이더리움은 보통 매 17초 마다

순서 선택 메커니즘("mining"이라고 불림)의 일부에서, 블록이 시시각각 반환되어질 수 있지만 그것은 오직 체인의 끝에서만 가능하다. 특정 블록의 top에 더 많은 블록이 추가될 수록, 이것은 더 적은 블록을 반환할 것이다. 그래서 블록체인으로부터 트랜잭션이 반환되거나 심지어 제거될 수 있다. 하지만 더 오래 기다릴 수록, 그러한일은 덜 발생될 것이다.

> 트랜잭션은 다음 블록 또는 미래의 특정 블록에 포함될 것이라고 보장할 수 없다.
왜냐하며 그것은 트랜잭션의 제출자(submitter)에게 달린 것이 아니라 채굴자(miner)가 그것을 블록에 포함시킬지 말지 결정하기 때문에 채굴자에게 달린것이다. 

만약 컨트랙트의 호출이 스케줄되길 원한다면 스마트컨트랙트 자동화 툴을 사용하거나 오라클 서비스를 사용할 수 있다.

# The Ethereum Virtual Machine

## Overview

Ethereum virtual machine 즉 EVM은 이더리움의 스마트컨트랙트를 위한 런타임 환경이다. 이것은 sandbox이자 완전히 고립된 것이다. 따라서 EVM 내부에서 작동하는 코드는 네트워크, 파일시스템 또는 다른 프로세스에 접근할 수 없다. 스마트 컨트랙트는 심지어 다른 스마트 컨트랙트로의 접근도 제한된다.

## Accounts

이더리움에는 동일한 주소 공간을 공유하는 두 종류의 계정이 있다.
- External Accounts : 공개-비공개키에 의해 제어
- Contract Accounts : 계정과 함께 저장된 코드에 의해 제어

external 계정의 주소는 컨트랙트가 생성될 당시 컨트랙트의 주소가 결정되는 동안 공개키로부터 결정되어진다. (이것은 컨트랙트 생성자 주소와 소위 "nonce"라고 불리는 주소로부터 보내진 트랜잭션의 숫자로부터 파생되어진다.)

계정에 코드가 저장되어 있는지 여부와 관계없이, 두 종류 다 EVM에 의해서 동일하게 다루어진다. 모든 계정은 지속적인 storage라고 불리는 key-value store mapping(256-bit word to 256-bit word)구조를 갖는다.

게다가, 모든 계정은 이더안에서 잔액을 가진다.
> “Wei” to be exact, 1 ether is 10**18 wei

이것은 이더를 포함한 트랜잭션을 전송함으로써 수정될 수 있다.











